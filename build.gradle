buildscript {
    repositories {
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        classpath 'com.github.johnrengelman:shadow:8.1.1'
    }
}

import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

subprojects {

    apply plugin: "java"
    apply plugin: "maven-publish"
    apply plugin: "com.github.johnrengelman.shadow"

    repositories {
        maven {
            url("http://repo.devpscl.de/repository/maven-public/")
            allowInsecureProtocol = true
        }
    }

    dependencies {
        implementation 'net.devpscl:jeventbus:1.1.0'
    }

    project.group = 'net.' + "jterminal"
    project.version = '1.0.0'

    compileJava.options.encoding = "UTF-8"
    compileTestJava.options.encoding = "UTF-8"

    tasks.register('buildTest', ShadowJar) {
        group = "jterminal"
        manifest {
            attributes["Main-Class"] = "net.jterminal.test.Main"
            attributes 'Multi-Release': 'true'
        }
        from sourceSets.main.output
        from sourceSets.test.output
        configurations = [project.configurations.testRuntimeClasspath]
        setArchiveClassifier "DEBUG"
    }

    tasks.register('buildRelease', ShadowJar) {
        group = "jterminal"
        manifest {
            attributes 'Multi-Release': 'true'
        }
        from sourceSets.main.output
        configurations = [project.configurations.testRuntimeClasspath]
        setArchiveClassifier "RELEASE"
    }

    println(project.name + " is configured")

    publishing {
        publications {
            maven(MavenPublication) {
                from components.java
            }
        }
        repositories {
            maven {
                url "http://devpscl.de:8081/repository/maven-releases/"
                credentials {
                    username credentialsUser
                    password credentialsPassphrase
                }
                allowInsecureProtocol = true
            }
        }
    }

}
